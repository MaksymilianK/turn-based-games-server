apply plugin: 'java'
apply plugin: 'application'

mainClassName = 'pl.konradmaksymilian.turnbasedgames.TurnBasedGamesApp'

sourceCompatibility = 10
targetCompatibility = 10

sourceSets {
	intTest {
		java.srcDir file('src/integration-test/java')
		resources.srcDir file('src/integration-test/resources')
		compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
	}
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
    mustRunAfter test
}

check.dependsOn integrationTest

configurations {
    intTestImplementation.extendsFrom testImplementation
    intTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: '2.0.4.RELEASE'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: '2.0.4.RELEASE'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-websocket', version: '2.0.4.RELEASE'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-mail', version: '2.0.4.RELEASE'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-logging', version: '2.0.4.RELEASE'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation', version: '2.0.4.RELEASE'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-hateoas', version: '2.0.4.RELEASE'
    implementation group: 'org.springframework.security', name: 'spring-security-messaging', version: '5.0.7.RELEASE'
    implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '2.6.2'
    implementation group: 'com.h2database', name: 'h2', version: '1.4.197'
    implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.0'
    
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.0.4.RELEASE'
    testImplementation group: 'org.springframework.security', name: 'spring-security-test', version: '5.0.7.RELEASE'
	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.2.0'
	testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.2.0'
	
}

repositories {
    mavenCentral()
}

test {
	useJUnitPlatform()
}

integrationTest {
	useJUnitPlatform()
}